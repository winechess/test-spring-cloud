buildscript {
    ext {
        springBootVersion = '1.5.2.RELEASE'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    }
}

group = "com.vinichenkosa.realty"
version = "0.0.1"

allprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'org.springframework.boot'

    repositories {
        jcenter()
        maven {
            url 'https://repo.spring.io/libs-milestone'
        }
    }

    dependencyManagement {
        imports {
            mavenBom 'org.springframework.cloud:spring-cloud-dependencies:Dalston.RC1'
        }
    }

    dependencies {

        compile 'org.springframework.boot:spring-boot-starter-web'
        compile 'org.springframework.boot:spring-boot-starter-actuator'
        compileOnly 'org.projectlombok:lombok:latest.release'

        testCompile 'org.springframework.boot:spring-boot-starter-test'
        testCompileOnly 'org.projectlombok:lombok:latest.release'

    }

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

}

subprojects {

    jar {
        archiveName = project.name + ".jar"
    }

    task dockerBuild {
        doLast {
            exec {
                commandLine 'docker', 'build', '--force-rm=true', "-t", "$project.name:$project.version", "-t", "$project.name:latest", '.'
            }
        }
    }

    dockerBuild.dependsOn build

}